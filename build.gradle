plugins {
	id 'java'
	id 'checkstyle'
	id 'jacoco'
	id 'org.springframework.boot' version '3.2.0'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'com.google.cloud.tools.jib' version '3.4.0'
	id "org.sonarqube" version "4.4.1.3373"
}

group = 'com.malli'
version = '0.0.1'
sourceCompatibility = '21'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.4'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

bootJar {
	archiveFileName.set(project.name + ".jar")
}

checkstyle {
	toolVersion = '10.12.0'
	configFile file("${project.rootDir}/src/main/resources/checkstyle/checkstyle.xml")
}

checkstyleMain {
	source = 'src/main/java'
}

checkstyleTest {
	source = 'src/test/java'
}

test {
	finalizedBy jacocoTestReport //report is always generated after tests run
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
	}
}
jib {
    from {
        image = 'amazoncorretto:21-alpine'
    }
    to {
        image = 'registry.hub.docker.com/mallireddy1998/' + project.name
    }
}